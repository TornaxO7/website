<!doctype html>
<html lang="en">
  <head data-vimmatic-content-status="ready">
    <meta charset="utf-8" />
    <title>How to wrap Shadertoy code?</title>
    <meta name="viewport" content="width=device-width, initial-scale=1" />
    <meta http-equiv="X-UA-Compatible" content="IE=edge" />

    <script src="https://xemantic.github.io/shader-web-background/dist/shader-web-background.min.js"></script>

    <script type="x-shader/x-fragment" id="Image">
      precision highp float;

      uniform vec2  iResolution;
      uniform float iTime;

      // Shadertoy-link https://www.shadertoy.com/view/XctBRN
      // made with an Art Of Code tutorial https://youtu.be/KGJUl8Teipk?si=6ZQdL4RyjtZsiQlz

      # define S smoothstep

      float DistLine(vec2 p, vec2 a, vec2 b){
          vec2 pa = p - a;
          vec2 ba = b - a;
          float t = clamp(dot(pa, ba)/dot(ba, ba), 0., 1.);
          return length(pa - ba*t);
      }

      float N21(vec2 p){
          p = fract(p*vec2(244.123, 231.532));
          p += dot(p, p+464.213);
          return fract(p.x*p.y);
      }

      vec2 N22(vec2 p){
          float n = N21(p+24.24);
          return vec2(n, N21(p+n));
          //return vec2(sin(p.x), cos(p.y));
      }

      vec2 GetPos(vec2 id, vec2 offs){
          vec2 n = N22(id + offs)*iTime;
          return offs + sin(n)*.4;
      }

      float Line(vec2 p, vec2 a, vec2 b){
          float d = DistLine(p, a, b);
          float m = S(.03, .01,  d);
          float d2 = length(a-b);
          m *= S(1.2, .5, d2);//*.5+S(.05, .03, abs(d2-.75));
          return m;
      }

      float Layer(vec2 uv){
          vec2 gv = fract(uv)-.5;
          vec2 id = floor(uv);

          float m = 0.;
          vec2 p[9];

          p[0] = GetPos(id, vec2(-1, -1));
          p[1] = GetPos(id, vec2(-1, 0));
          p[2] = GetPos(id, vec2(-1, 1));

          p[3] = GetPos(id, vec2(0, -1));
          p[4] = GetPos(id, vec2(0, 0));
          p[5] = GetPos(id, vec2(0, 1));

          p[6] = GetPos(id, vec2(1, -1));
          p[7] = GetPos(id, vec2(1, 0));
          p[8] = GetPos(id, vec2(1, 1));

          float t = iTime*10.;
          for (int i = 0;i<9; i++){
              m += Line(gv, p[4], p[i]);
              vec2 j = (p[i] - gv)*20.;
              float sparkle = 1./dot(j, j);
              m += sparkle*(sin(t+fract(p[i]).x*10.)*.5+.5)*.8;
          }

          m += Line(gv, p[1], p[3]);
          m += Line(gv, p[1], p[5]);
          m += Line(gv, p[3], p[7]);
          m += Line(gv, p[5], p[7]);
          return m;
      }

      void mainImage( out vec4 fragColor, in vec2 fragCoord )
      {
          vec2 uv = (fragCoord-.5*iResolution.xy)/iResolution.y;
          float m = 0.;
          float t = iTime*.1;

          float gradient = uv.y;
          float s = sin(t);
          float c = cos(t);
          mat2 rot = mat2(c, -s, s, c);
          uv *= rot;

          for(float i = 0.; i <=1.; i+=1./5.){
              float z = fract(i+t);
              float size = mix(10., .5, z);
              float fade = S(0., .5, z)*S(1., .8, z);
              m += Layer(uv*size+i*15.)*fade;

          }

          vec3 base = sin(t*5.*vec3(.234, .675, .862))*.4+.6;

          vec3 col = m*base;//*.1;
          col -= gradient*base;

          fragColor = vec4(col,1.0);
      }

      // -- End of Shadertoy code

      void main() {
        mainImage(gl_FragColor, gl_FragCoord.xy);
      }
    </script>
    <script>
      shaderWebBackground.shade({
        shaders: {
          Image: {
            uniforms: {
              iResolution: (gl, loc, ctx) =>
                gl.uniform2f(loc, ctx.width, ctx.height),
              iTime: (gl, loc) => gl.uniform1f(loc, performance.now() / 1000),
            },
          },
        },
      });
    </script>
  </head>
  <body>
    <header>
      <h1><a href="#source">How to wrap Shadertoy code?</a></h1>
    </header>
  </body>
</html>
