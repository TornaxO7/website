<script src="https://xemantic.github.io/shader-web-background/dist/shader-web-background.min.js"></script>

<script type="x-shader/x-fragment" id="Image">
  precision highp float;

  uniform vec2  iResolution;
  uniform float iTime;

    #define PI 3.1415926

  float rand21(vec2 p) {
      return fract(sin(dot(p, vec2(123.456, 456.789))) * 789.123);
  }

  float drawTruchet(vec2 gv, float width_mod) {
      float m = 0.;

      float radius = .5;
      float width = .01 * width_mod;
      float w2 = width * .5;
      float blur = .03;

      vec2 tr_corner = gv + .5;
      vec2 bl_corner = gv - .5;

      m += smoothstep(radius + w2 + blur, radius + w2, length(tr_corner));
      m -= smoothstep(radius - w2, radius - w2 - blur, length(tr_corner));

      m += smoothstep(radius + w2 + blur, radius + w2, length(bl_corner));
      m -= smoothstep(radius - w2, radius - w2 - blur, length(bl_corner));

      return m;
  }

  mat2 getRotationMatrix(float r) {
      return mat2(cos(r), -sin(r), sin(r), cos(r));
  }

  void mainImage( out vec4 fragColor, in vec2 fragCoord ) {
      float time = iTime * .05;
      float zoom = 10.;
      vec3 base_color1 = .4 * sin(vec3(.18, .948, .481) + iTime) + .5;
      vec3 base_color2 = .3 * sin(vec3(.123, .456, .789) + iTime) + .7;
      vec3 color = vec3(0.);

      // == uvs
      vec2 orig_uv = fragCoord.xy/iResolution.xy;
      orig_uv.x *= iResolution.x / iResolution.y;
      orig_uv.y = 1. -orig_uv.y;

      vec2 uv = orig_uv;
      uv *= getRotationMatrix(time);
      uv -= .5;
      uv *= zoom;

      vec2 uv2 = orig_uv;
      uv2 *= getRotationMatrix(4.);
      uv2 *= zoom;
      // == uvs end

      vec2 gv = fract(uv) - .5;
      vec2 id = floor(uv);

      // draw the big circles
      float x = sin(length(uv2) - iTime * .4);
      float y = abs(.2 / (x * 1.3)) - .14;
      color += base_color1 * y;

      // draw the truchet pattern
      float width_mod = 50. * y;
      if (step(rand21(id), .5) > 0.) {
          gv.x = -gv.x;
          color += base_color2 * drawTruchet(gv, width_mod);
      } else {
          color += base_color2 * drawTruchet(gv, width_mod);
      }


      fragColor = vec4(color,1.0);
  }

  // -- End of Shadertoy code

  void main() {
    mainImage(gl_FragColor, gl_FragCoord.xy);
  }
</script>
<script>
  shaderWebBackground.shade({
    shaders: {
      Image: {
        uniforms: {
          iResolution: (gl, loc, ctx) =>
            gl.uniform2f(loc, ctx.width, ctx.height),
          iTime: (gl, loc) => gl.uniform1f(loc, performance.now() / 1000),
        },
      },
    },
  });
</script>
